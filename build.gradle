buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}
plugins {
    id "org.asciidoctor.convert" version "1.5.9.2"
    id "org.ajoberstar.git-publish" version "2.0.0"
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


dependencies {
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.3.RELEASE'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.3.RELEASE'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}


group = 'com.yangxiaochen'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    maven {
        url "http://maven.aliyun.com/nexus/content/groups/public"
    }
}


dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')

    annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')
    annotationProcessor('org.projectlombok:lombok')
    compileOnly('org.projectlombok:lombok')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    testCompile 'com.google.guava:guava:27.0-jre'

}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}



gitPublish {
    // where to publish to (repo must exist)
    repoUri = 'git@github.com:yxc023/spring-restdoc-demo.git'
    branch = 'gh-pages'

    // what to publish, this is a standard CopySpec
    contents {
        from ("$buildDir/asciidoc/html5")
    }

    // what to keep in the existing branch (include=keep)
    preserve {
        include '.gitmodules'
    }

    // message used when committing changes
    commitMessage = 'Generated by gradle-git-publish' // defaults to 'Generated by gradle-git-publish'

}

gitPublishCopy {
    dependsOn asciidoctor
}

asciidoctor {
    resources {
        from('src/docs/asciidoc/images') {
            into './images'
        }
        from('src/docs/asciidoc/stylesheets') {
            into './stylesheets'
        }
    }

    attributes 'source-highlighter' : 'prettify',
                icons               : 'font'
}


task("p") {
    group "publishing"
    description "short command for gitPublishPush"
    dependsOn gitPublishPush
}